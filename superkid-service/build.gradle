apply from: '../build.gradle'
apply plugin: 'application'

mainClassName = "us.jsource.superkid.SuperkidServiceApplication"

dependencies {
    compile('org.springframework.cloud:spring-cloud-starter-netflix-eureka-client')
    compile('org.springframework.cloud:spring-cloud-starter-config')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-jdbc')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile("org.springframework.retry:spring-retry:${springRetryVersion}")
    compile("javax.xml.bind:jaxb-api:${jaxbVersion}")
    runtime('org.postgresql:postgresql')
    compileOnly('org.projectlombok:lombok')
}

compileJava {
    options.compilerArgs += '-proc:none'
}

import com.bmuschko.gradle.docker.tasks.image.Dockerfile
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

task createDockerfile(type: Dockerfile) {
    destFile = project.file('build/libs/Dockerfile')
    from 'openjdk:9'
    instruction 'RUN mkdir usr/local/superkid-service'
    copyFile("superkid-service-${version}.jar", "/usr/local/superkid-service-${version}.jar")
    entryPoint 'java'
    defaultCommand '-jar',
            '-Dspring.profiles.active=${SPRING_PROFILES_ACTIVE}',
            '-Deureka.client.service-url.defaultZone=${EUREKA_CLIENT_SERVICE_URL_DEFAULTZONE}',
            '-Dspring.datasource.url=${SPRING_DATASOURCE_URL}',
            '-Dspring.datasource.username=${SPRING_DATASOURCE_USERNAME}',
            '-Dspring.datasource.password=${SPRING_DATASOURCE_PASSWORD}',
            "/usr/local/superkid-service-${version}.jar"
}

task buildImage(type: DockerBuildImage) {
    dependsOn createDockerfile
    inputDir = createDockerfile.destFile.parentFile
    tag = "vadimdissa/superkid-service:${version}"
}

build.finalizedBy(buildImage)